<!doctype html public "-//W3C//DTD HTML 3.2//EN">
<html>
<head>
    <title>Commodore 1571 firmware source code: Motor and stepper control</title><meta name="viewport" content="width=device-width, initial-scale=1">
</head>

<body>
<a href="index.html"><img src="../../../../images/blue_toc.gif" width="24" height="24" alt="[Contents]"></a> <a href="../../index.html"><img src="../../../../images/blue_cbm.gif" alt="[Commodore]" width="22" height="24"></a>
 <a href="https://www.devili.iki.fi/general/new.html" title="New"><img src="../../../../images/blue_new.gif" alt="[New]" height="24" width="27"></a>
<a href="https://www.devili.iki.fi/search/" title="Search"><img src="../../../../images/blue_search.gif" alt="[Search]" height="24" width="24"></a>
<a href="https://www.devili.iki.fi/" title="Home"><img src="../../../../images/blue_home.gif" alt="[Home]" width=24 height=24></a>
<br>
<hr>


<center>
<h3>Commodore 1571 firmware<br>
    Motor and stepper control</h3>
</center>

<pre>
        .page  
	.subttl 'lccend.src'
;
;
;
;   motor and stepper control
;
;
;   irq into controller every  8.224 ms
end              
        lda  t1hl2      ; set irq timer
        sta  t1hc2       
;
        lda  dskcnt      
        and  #$10       ;  test write proctect
        cmp  lwpt        
        sta  lwpt       ;  change ?

;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;

	jmp  ptch21	;  do auto on code *** rom ds 85 ***
	nop		;  fill
	nop		;  fill
	nop		;  fill
	
;       beq  end002     ;  no

;       lda  #1         ;  yes, set flag
;       sta  wpsw        
;

rtch21			; return

;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;&lt;&gt;

end002  lda  phase      ;  test for phase offset
        beq  end40       
;
        cmp  #2          
        bne  end003      
;
        lda  #0         ; phase &lt;-- 0
        sta  phase       
        beq  end40       
;
end003  sta  steps       
        lda  #2         ; phase &lt;-- 2
        sta  phase       
        jmp  dostep      
;
end40   ldx  cdrive     ;  work on active drive only
        bmi  end33x     ;  no active drive
;
        lda  drvst      ;  test if motor on
        tay      
        cmp  #$20       ;  test if anything to do
        bne  end10      ;  something here
;
end33x  jmp  end33      ;  motor just running
;
end10   dec  acltim     ;  dec timer
        bne  end30       
;
        tya     	;  test if acel
        bpl  end20       
;
;
        and  #$7f       ;  over, clear acel bit
        sta  drvst       
;
end20   and  #$10       ;  test if time out state
        beq  end30       

	dec  acltim2	;  timer2 *rom-05ds 85*
	bne  end30
	nop		;  fill
	jsr  motoff	;  off the motor, ok...
;
;       lda  dskcnt     ;        
;	and  #$ff-$04   ;  turnoff motor
;       sta  dskcnt      
;
;
        lda  #$ff       ;  no active drive now
        sta  cdrive      
;
        lda  #0         ;  drive inactive
        sta  drvst      ;  clear on bit and timout
        beq  end33x      
;
end30   tya     	;  test if step needed
        and  #$40        
        bne  end30x     ;  stepping
;
        jmp  end33       
;
;
end30x  jmp  (nxtst)    ; goto proper stepper state
;
inact   lda  steps      ;  get abs(steps)
        bpl  inac10      
;
        eor  #$ff        
        clc      
        adc  #1          
;
inac10  cmp  minstp     ;  test if we can accel
        bcs  inac20     ;  too small
;
        lda  #&lt;short    ; short step mode
        sta  nxtst       
        lda  #&gt;short     
        sta  nxtst+1     
        bne  dostep      
;
inac20          	;  calc the # of run steps
        sbc  as          
        sbc  as          
        sta  rsteps      
;
        lda  as          
        sta  aclstp     ;  set  # of accel steps
        lda  #&lt;ssacl     
        sta  nxtst       
        lda  #&gt;ssacl
        sta  nxtst+1     
;
dostep  lda  steps       
        bpl  stpin       
;
;---------rom05-bc 84--------
;stpout inc steps
;	ldx dskcnt
;	dex
;	jmp stp

stpout  jmp  patch9     ; check track 0 (photo sensor)
        nop     	; 
        nop     	; 
        nop     	; 
pppppp  jmp  stp        ; goto step
;----------------------------------
;
short   lda  steps      ; step end ?
        bne  dostep     ; no
;
        lda  #&lt;setle    ; settle
        sta  nxtst       
        lda  #&gt;setle     
        sta  nxtst+1     
        lda  #5         ;  settle time (5*8=40ms)
        sta  aclstp      
        jmp  end33       
;
setle   dec  aclstp     ;  settle end ?
        bne  end33      ;  no
;
        lda  drvst       
        and  #$ff-$40    
        sta  drvst       
;
        lda  #&lt;inact     
        sta  nxtst       
        lda  #&gt;inact     
        sta  nxtst+1     
        jmp  end33       
;
stpin   dec  steps       
        ldx  dskcnt      
        inx      
;
stp     txa      
        and  #3          
        sta  tmp         
        lda  dskcnt      
        and  #$ff-$03   ;  mask out old
        ora  tmp         
        sta  dskcnt      
        jmp  end33       
;
ssacl           	;  sub acel factor
        sec      
        lda  t1hl2       
        sbc  af          
        sta  t1hc2       
;
        dec  aclstp      
        bne  ssa10       
;
        lda  as          
        sta  aclstp      
;
        lda  #&lt;ssrun     
        sta  nxtst       
        lda  #&gt;ssrun     
        sta  nxtst+1     
;
ssa10   jmp  dostep      
;
ssrun   dec  rsteps      
        bne  ssa10       
;
        lda  #&lt;ssdec     
        sta  nxtst       
        lda  #&gt;ssdec     
        sta  nxtst+1     
        bne  ssa10       
;
ssdec           	;  decel
        lda  t1hl2       
        clc      
        adc  af          
        sta  t1hc2       
;
        dec  aclstp      
        bne  ssa10       
;
        lda  #&lt;setle    ;  goto settle mode
        sta  nxtst       
        lda  #&gt;setle     
        sta  nxtst+1     
;
        lda  #5          
        sta  aclstp     ;  settle timer
;
;
end33   lda  pcr2       ;  disable s.o. to 6502
        and  #$ff-$02    
        sta  pcr2        
;
        rts      
;.end
</pre>

<hr>
 <a href="index.html"><img src="../../../../images/blue_toc.gif" width="24" height="24" alt="[Contents]"></a> <a href="../../index.html"><img src="../../../../images/blue_cbm.gif" alt="[Commodore]" width="22" height="24"></a>
 <a href="https://www.devili.iki.fi/general/new.html" title="New"><img src="../../../../images/blue_new.gif" alt="[New]" height="24" width="27"></a>
<a href="https://www.devili.iki.fi/search/" title="Search"><img src="../../../../images/blue_search.gif" alt="[Search]" height="24" width="24"></a>
<a href="https://www.devili.iki.fi/" title="Home"><img src="../../../../images/blue_home.gif" alt="[Home]" width=24 height=24></a>
<br>
<table cellspacing="0" border="0" width="100%">
<tr><td align="right" colspan="2"><small>
	This page has been created by <a href="mailto:rtiainen@suespammers.org">
	Sami Rautiainen</a>.
</small></td></tr><tr><td align="left"><small>
	Read the <a href="https://www.devili.iki.fi/general/copyright.html">small print</a>.
</small></td><td align="right"><small>
	Last updated 
	September 05, 2020.
</small></td></tr>
</table>


</body>
</html>

